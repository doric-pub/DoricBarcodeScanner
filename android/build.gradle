import groovy.json.JsonSlurper

def model = new JsonSlurper().parse(new File(project.getProjectDir().parent + File.separator + "package.json"))
def doricSDKVersion = model.dependencies.doric.replace("^", "").replace(">=", "")

println("Doric Version:" + doricSDKVersion)

buildscript {
    ext.kotlin_version = '1.3.61'
    ext.protobuf_version = '0.8.8'
    repositories {
        mavenCentral()
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

rootProject.allprojects {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 29

    sourceSets {
        main.assets.srcDirs += "../dist"
    }
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
    }
}

dependencies {
    api "pub.doric:core:$doricSDKVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'me.dm7.barcodescanner:zxing:1.9.13'
}
